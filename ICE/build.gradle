// gradle clean build publishToMavenLocal

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

java {
    modularity.inferModulePath = true
}

sourceCompatibility = '11'
targetCompatibility = '11'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

javadoc {
    options.noTimestamp = true;
    options.noQualifiers = ["all"];
    options.noTree = true;
    options.noIndex = true;
    options.noNavBar = true;
}

def releaseFileName = "${rootProject.name}-${version}.zip"
def releaseDir = "$projectDir/release"
def releaseOutDir = "${buildDir}/release/${rootProject.name}"

task copyLibs(type: Copy) {
    from "$projectDir/libs"
    from jar.archiveFile
    into "${releaseOutDir}/libs"
}

task createRelease(type: Copy, dependsOn: copyLibs) {
    from "${project.rootDir}/README.md"
    into "${releaseOutDir}"
}

task packageDistribution(type: Zip) {
    archiveFileName = releaseFileName
    destinationDirectory = file("$projectDir/release")
    from file("$releaseOutDir")
}

task cleanRelease(type: Delete) {
    delete "$releaseDir"
    delete "$releaseOutDir"
}

task packageJavadoc(type: Copy) {
    from file("$projectDir/build/docs")
    into file("${project.rootDir}/docs")
}

packageDistribution.dependsOn createRelease
createRelease.dependsOn javadoc
build.dependsOn packageDistribution, packageJavadoc
clean.dependsOn cleanRelease
